import org.jetbrains.kotlin.gradle.dsl.JvmTarget

buildscript {
    ext.kotlin_version = '2.2.20'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

group 'com.olo.flutter.digitalwallets'
version '1.0-SNAPSHOT'

// Only load settings need to compile this module by itself if we are
// NOT running this in the context of a larger project
def standaloneFile = new File("load-standalone-module-settings.gradle")
if (standaloneFile.exists() && rootProject == project) {
    apply from: "load-standalone-module-settings.gradle"
}

android {
    namespace 'com.olo.flutter.digitalwallets'

    compileSdk = 36

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    defaultConfig {
        minSdk = 26
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }

        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation 'com.olo.pay:digitalwallets-android-sdk:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.1'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.1'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'org.mockito:mockito-android:5.20.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.1'

    if (rootProject.hasProperty('addFlutterEngineDependency') && rootProject.addFlutterEngineDependency) {
        rootProject.addFlutterEngineDependency(dependencies)
    }

    // Add Flutter engine for androidTest
    if (project.hasProperty('standaloneFlutterEnginePath')) {
        androidTestImplementation files(project.property('standaloneFlutterEnginePath'))
    }
}
