// This script loads local standalone module settings to enable compiling of the Android SDK
// Flutter module without having to run it through the Flutter app

def flutterSdkPath = ""

// Check for a local.properties file and load properties if they exist
def localPropertiesFile = new File('local.properties')
if (localPropertiesFile.exists()) {
    def properties = new Properties()
    localPropertiesFile.withInputStream { stream ->
        properties.load(stream)
    }

    if (flutterSdkPath == "" && properties.containsKey('flutter.sdk')) {
        flutterSdkPath = properties['flutter.sdk']
    }
}

if (flutterSdkPath == "") {
    printWarning("FLUTTER SDK PATH NOT SET")
} else if (!file(flutterSdkPath).exists()) {
    printWarning("BASE FLUTTER SDK PATH DOES NOT EXIST")
} else {
    def flutterEnginePath = "${flutterSdkPath}/bin/cache/artifacts/engine/${getHostArchName()}/flutter.jar"
    if (!file(flutterEnginePath).exists()) {
        printWarning("FLUTTER ENGINE PATH DOES NOT EXIST\n${flutterEnginePath}")
    } else {
        rootProject.ext.standaloneFlutterEnginePath = flutterEnginePath
        println "======================================"
        println "FLUTTER ENGINE PATH SET"
        println "${rootProject.ext.standaloneFlutterEnginePath}"
        println "======================================\n"
    }
}

static String getHostArchName() {
    def os = System.getProperty("os.name").toLowerCase()
    def arch = System.getProperty("os.arch")

    if (os.contains("mac")) {
        return arch == "aarch64" ? "android-arm64-release" : "android-x64-release"
    }
    if (os.contains("linux")) {
        return arch == "aarch64" ? "android-arm64-release" : "android-x64-release"
    }
    if (os.contains("win")) {
        return "android-x64-release"
    }
    return "android-x64-release"
}

static void printWarning(String warning) {
    println "======================================"
    println "WARNING: ${warning}\n"
    println "This is the sub-module project and cannot be directly"
    println "compiled unless the flutter.sdk property is specified"
    println "in local.properties"
    println "======================================\n"
}

rootProject.ext.addFlutterEngineDependency = { dependencies ->
    if (rootProject.hasProperty('standaloneFlutterEnginePath') && rootProject.standaloneFlutterEnginePath) {
        dependencies.compileOnly files("${rootProject.ext.standaloneFlutterEnginePath}")
        dependencies.testImplementation files("${rootProject.ext.standaloneFlutterEnginePath}")

        println "======================================"
        println "FLUTTER ENGINE COMPILE DEPENDENCIES ADDED"
        println("Path: ${rootProject.ext.standaloneFlutterEnginePath}")
        println "======================================\n"
    }
    else {
        println "======================================"
        println "FLUTTER ENGINE DEPENDENCIES MISSING"
        println "======================================"
    }
}